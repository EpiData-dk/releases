; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "EpiData Software Suite"
#define MyAppVersion "{{ VERSION }}"
#define MyAppPublisher "EpiData Association"
#define MyAppURL "http://www.epidata.dk"
#define MyAppExeName 

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
AppId={{18959778-3966-4463-B64C-0E1CB44181E3}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\EpiData
DefaultGroupName=EpiData
AllowNoIcons=yes
OutputBaseFilename=epidata-all-in-one-setup.{#MyAppVersion}
Compression=lzma2
SolidCompression=yes
InfoAfterFile=readme.txt
LicenseFile=license.txt

[Types]
Name: "full";        Description: "Complete Installation"
Name: "manager";     Description: "Install EpiData Manager"
Name: "entryclient"; Description: "Install EpiData EntryClient"
Name: "analysis";    Description: "Install EpiData Analysis"
Name: "classic";     Description: "Install EpiData Classic Analysis"
Name: "custom";      Description: "Custom Installation"; Flags: iscustom

[Components]
Name: "Manager";     Description: "EpiData Manager";          Types: full manager     custom;
Name: "EntryClient"; Description: "EpiData EntryClient";      Types: full entryclient custom;
Name: "Analysis";    Description: "EpiData Analysis";         Types: full analysis    custom;
Name: "Classic";     Description: "EpiData Classic Analysis"; Types: full classic     custom;
Name: "Help";        Description: "Help Files";               Types: full manager entryclient analysis classic;
Name: "Examples";    Description: "Examples";                 Types: full manager entryclient analysis classic;

[Tasks]
Name: "DesktopIcon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}";

[Files]
Source: "epidatamanager.exe";     DestDir: "{app}"; DestName: "epidatamanager.exe";     Check: IsWin64;     Components: Manager;     Flags: ignoreversion
Source: "epidataentryclient.exe"; DestDir: "{app}"; DestName: "epidataentryclient.exe"; Check: IsWin64;     Components: EntryClient; Flags: ignoreversion
Source: "epidataanalysis.exe";    DestDir: "{app}"; DestName: "epidataanalysis.exe";    Check: IsWin64;     Components: Analysis;    Flags: ignoreversion
Source: "*.dll";                  DestDir: "{app}";                                     Check: IsWin64;     Components: Manager EntryClient Analysis;

; ; Classic - install both 32 and 64 bit
; Source: "system\EpiDataStat.exe"; DestDir: "{app}";                   Components: Classic; Flags: ignoreversion
; Source: "system\images\*";        DestDir: "{app}\languages\images\"; Components: Classic; Flags: ignoreversion
; Source: "css\*.*";                DestDir: "{app}";                   Components: Classic; Flags: ignoreversion
; Source: "lang\en\system\*";       DestDir: "{app}";                   Components: Classic; Flags: ignoreversion
; Source: "lang\en\start\*";        DestDir: "{app}\languages\en\";     Components: Classic; Flags: ignoreversion
; Source: "lang\en\help\*";         DestDir: "{app}\languages\en\help"; Components: Classic; Flags: ignoreversion


; Documentation
; - Common
Source: "docs\ExtendedAccess.pdf";                                            DestDir: "{app}\docs";     Components: Help;

; - Manager
Source: "docs\epidatamanagerintro.pdf";                                       DestDir: "{app}\docs";     Components: Manager and Help;
Source: "docs\epidatamanagershortcuts.pdf";                                   DestDir: "{app}\docs";     Components: Manager and Help;

; - EntryClient
Source: "docs\epidataentryclientintro.pdf";                                   DestDir: "{app}\docs";     Components: EntryClient and Help;
Source: "docs\epidataentryclientshortcuts.pdf";                               DestDir: "{app}\docs";     Components: EntryClient and Help;

; - Analysis
Source: "docs\How_to_adapt_your_pgm_file_to_new_EpiData_Analysis_format.pdf"; DestDir: "{app}\docs";     Components: Analysis and Help;
Source: "docs\commands.html";                                                 DestDir: "{app}\docs";     Components: Analysis and Help;
Source: "docs\EpiDataAnalysis_Introduction.pdf";                              DestDir: "{app}\docs";     Components: Analysis and Help;

; Examples
Source: "examples\*";                                                         DestDir: "{app}\examples"; Components: Examples;

[Icons]
Name: "{group}\EpiData Manager";                    Filename: "{app}\epidatamanager.exe";     Components: Manager
Name: "{group}\EpiData EntryClient";                Filename: "{app}\epidataentryclient.exe"; Components: EntryClient
Name: "{group}\EpiData Analysis";                   Filename: "{app}\epidataanalysis.exe";    Components: Analysis
Name: "{group}\EpiData Analysis Classic";           Filename: "{app}\EpiDataStat.exe";        Components: Classic;
Name: "{commondesktop}\EpiData Manager";            Filename: "{app}\epidatamanager.exe";     Components: Manager;     Tasks: DesktopIcon
Name: "{commondesktop}\EpiData EntryClient";        Filename: "{app}\epidataentryclient.exe"; Components: EntryClient; Tasks: DesktopIcon
Name: "{commondesktop}\EpiData Analysis";           Filename: "{app}\epidataanalysis.exe";    Components: Analysis;    Tasks: DesktopIcon
Name: "{commondesktop}\EpiData Analysis Classic";   Filename: "{app}\EpiDataStat.exe";        Components: Classic;     Tasks: DesktopIcon
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
                                                                                                                                                      

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"